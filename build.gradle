buildscript {
    ext {
        springBootVersion = '2.0.0.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.20.1")
    }
}

group 'it.cbmz.raspo'
version '0.1'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.palantir.docker'
apply plugin: 'com.palantir.docker-compose'


mainClassName = "it.cbmz.raspo.backend.RaspoApplication"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-webflux'
    compile 'org.springframework.kafka:spring-kafka:2.1.5.RELEASE'
    compile group: 'org.apache.kafka', name: 'kafka-streams', version: '1.0.1'
    compile 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
    runtime 'org.springframework.boot:spring-boot-devtools'
    compileOnly 'org.projectlombok:lombok:1.16.20'
}


task npmDependencies(type: Exec) {
    workingDir 'src/main/ui'
    commandLine 'npm'
    args = ["install"]
}

task buildUI(type: Exec, dependsOn: npmDependencies) {
    workingDir 'src/main/ui'
    commandLine 'npm'
    args = ["run", "build"]
}

processResources.dependsOn buildUI

docker {
    dependsOn build
    name "${project.group}/${bootJar.baseName}"
    tags ("latest", "${project.version}")
    files bootJar.archivePath
    buildArgs(['JAR_FILE': "${bootJar.archiveName}"])
}
