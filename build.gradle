buildscript {
    ext {
        springBootVersion = '2.0.0.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'


group 'it.cbmz.raspo'
version '1.0-SNAPSHOT'

mainClassName = "it.cbmz.raspo.backend.RaspoApplication"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-webflux'
    compile 'org.springframework.kafka:spring-kafka:2.1.5.RELEASE'
    compile group: 'org.apache.kafka', name: 'kafka-streams', version: '1.0.1'
    compile 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
    runtime 'org.springframework.boot:spring-boot-devtools'
    compileOnly 'org.projectlombok:lombok:1.16.20'
}


task npmDependencies(type: Exec) {
    workingDir 'src/main/ui'
    commandLine 'npm'
    args = ["install"]
}

task buildUI(type: Exec, dependsOn: npmDependencies) {
    logger.lifecycle("-" * 100)
    logger.lifecycle("""
    THIS BUILD HAS BEEN TESTED ON Ubuntu 17.10 WITH NPM 5.6.0 AND NODE v8.10.0 AVAILABLE ON THE PATH
""")
    logger.lifecycle("-" * 100)
    workingDir 'src/main/ui'
    commandLine 'npm'
    args = ["run", "build"]
}

processResources.dependsOn buildUI
sourceSets { main { java { exclude 'it/cbmz/raspo/backend/kafka/**' }}}
